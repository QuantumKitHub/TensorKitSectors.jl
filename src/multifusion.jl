# Rep Z2 âŠ• Rep Z2 â‰… Ising is worked out here
# ğ’ = ğ’Ÿ = RepZ2 â‰… {1, Ïˆ}, while â„³ = Vec â‰… {Ïƒ}
# this is mainly meant for testing within TensorKit without relying on MultiTensorKit
#------------------------------------------------------------------------------#

@enum CatType ğ’ = 1 â„³ = 3 â„³áµ’áµ– = 2 ğ’Ÿ = 4
# possible TODO: get rid of CatType and use Int instead -> prevents the need to export ğ’ etc
function Base.getindex(a::CatType, label::Int)
    return IsingBimod(a, label)
end

struct IsingBimod <: Sector
    type::CatType
    label::Int
    function IsingBimod(type::CatType, label::Int)
        if label < 0 || label > 1 || (label == 1 && (type == â„³ || type == â„³áµ’áµ–))
            throw(ArgumentError(lazy"Invalid $type label for Ising bimodule: $(label)"))
        end
        return new(type, label)
    end
end

isC(x::IsingBimod) = x.type == ğ’
isM(x::IsingBimod) = x.type == â„³
isMop(x::IsingBimod) = x.type == â„³áµ’áµ–
isD(x::IsingBimod) = x.type == ğ’Ÿ

function ismodulecategory(a::IsingBimod)
    return a.type in (â„³, â„³áµ’áµ–)
end

const all_isingbimod_objects = (IsingBimod(ğ’, 0), IsingBimod(ğ’, 1), IsingBimod(â„³áµ’áµ–, 0),
                                IsingBimod(â„³, 0), IsingBimod(ğ’Ÿ, 0), IsingBimod(ğ’Ÿ, 1))

Base.IteratorSize(::Type{<:SectorValues{IsingBimod}}) = Base.SizeUnknown()
Base.iterate(::SectorValues{IsingBimod}, i=1) = iterate(all_isingbimod_objects, i)
Base.length(::SectorValues{IsingBimod}) = length(all_isingbimod_objects)

âŠ—(a::IsingBimod, b::IsingBimod) = IsingBimodIterator(a, b)

struct IsingBimodIterator
    a::IsingBimod
    b::IsingBimod
end

Base.IteratorSize(::Type{IsingBimodIterator}) = Base.SizeUnknown()
Base.IteratorEltype(::Type{IsingBimodIterator}) = Base.HasEltype()
Base.eltype(::Type{IsingBimodIterator}) = IsingBimod

function Base.iterate(iter::IsingBimodIterator, state=0)
    if isC(iter.a) && isC(iter.b) # ğ’ Ã— ğ’ -> ğ’
        return state == 0 ? (IsingBimod(ğ’, mod(iter.a.label + iter.b.label, 2)), 1) :
               nothing
    end

    if isD(iter.a) && isD(iter.b) # ğ’Ÿ Ã— ğ’Ÿ -> ğ’Ÿ
        return state == 0 ? (IsingBimod(ğ’Ÿ, mod(iter.a.label + iter.b.label, 2)), 1) :
               nothing
    end

    if isM(iter.a) && isMop(iter.b) # â„³ Ã— â„³op -> ğ’
        return state < 2 ? (IsingBimod(ğ’, state), state + 1) : nothing
    end

    if isMop(iter.a) && isM(iter.b) # â„³op Ã— â„³ -> ğ’Ÿ
        return state < 2 ? (IsingBimod(ğ’Ÿ, state), state + 1) : nothing
    end

    if isC(iter.a) && isM(iter.b) # ğ’ Ã— â„³ -> â„³
        return state == 0 ? (iter.b, 1) : nothing
    end

    if isMop(iter.a) && isC(iter.b) # â„³op x ğ’ -> â„³op
        return state == 0 ? (iter.a, 1) : nothing
    end

    if isM(iter.a) && isD(iter.b) # â„³ x ğ’Ÿ -> â„³
        return state == 0 ? (iter.a, 1) : nothing
    end

    if isD(iter.a) && isMop(iter.b) # ğ’Ÿ x â„³op -> â„³op
        return state == 0 ? (iter.b, 1) : nothing
    end

    return nothing
end

function Base.convert(::Type{IsingAnyon}, a::IsingBimod) # identify RepZ2 âŠ• RepZ2 â‰… Ising
    ismodulecategory(a) && return IsingAnyon(:Ïƒ)
    return IsingAnyon(a.label == 0 ? :I : :Ïˆ)
end

FusionStyle(::Type{IsingBimod}) = SimpleFusion() # no multiplicities
BraidingStyle(::Type{IsingBimod}) = NoBraiding() # because of module categories

function Nsymbol(a::IsingBimod, b::IsingBimod, c::IsingBimod)
    # if a and b can fuse, then so can dual(a) and c, and c and dual(b)
    # only needs to be explicitly checked when CatTypes differ or when there's a module category involved
    if a.type != b.type || a.type != c.type || b.type != c.type ||
       any(ismodulecategory, (a, b, c))
        c âˆˆ a âŠ— b && dual(b) âˆˆ dual(c) âŠ— a && dual(a) âˆˆ b âŠ— dual(c) ||
            throw(ArgumentError("invalid fusion channel"))
    end
    return Nsymbol(convert(IsingAnyon, a), convert(IsingAnyon, b), convert(IsingAnyon, c))
end

function Fsymbol(a::I, b::I, c::I, d::I, e::I, f::I) where {I<:IsingBimod}
    Nsymbol(a, b, e) && Nsymbol(e, c, d) &&
        Nsymbol(b, c, f) && Nsymbol(a, f, d) || return 0.0
    return Fsymbol(convert(IsingAnyon, a), convert(IsingAnyon, b), convert(IsingAnyon, c),
                   convert(IsingAnyon, d), convert(IsingAnyon, e), convert(IsingAnyon, f))
end

function Base.conj(a::IsingBimod) # â„³ â†” â„³op when conjugating elements within these
    if isC(a) || isD(a) # self-conjugate within RepZ2
        return a
    elseif isM(a)
        return IsingBimod(â„³áµ’áµ–, a.label)
    else
        return IsingBimod(â„³, a.label)
    end
end

function rightone(a::IsingBimod)
    if isC(a) || isD(a)
        return IsingBimod(a.type, 0)
    elseif isM(a) # â„³ as right-ğ’Ÿ module category
        return IsingBimod(ğ’Ÿ, 0)
    else
        return IsingBimod(ğ’, 0)
    end
end

function leftone(a::IsingBimod)
    if isC(a) || isD(a)
        return IsingBimod(a.type, 0)
    elseif isM(a) # â„³ as left-ğ’ module category
        return IsingBimod(ğ’, 0)
    else
        return IsingBimod(ğ’Ÿ, 0)
    end
end

function Base.one(a::IsingBimod)
    if isC(a) || isD(a)
        return IsingBimod(a.type, 0)
    else
        throw(DomainError("unit of module category $(a.type) doesn't exist"))
    end
end

Base.one(::Type{IsingBimod}) = throw(ArgumentError("one of Type IsingBimod doesn't exist"))

function Base.show(io::IO, ::MIME"text/plain", a::IsingBimod)
    if isC(a)
        print(io, "ğ’[$(a.label)]")
    elseif isM(a)
        print(io, "â„³[$(a.label)]")
    elseif isMop(a)
        print(io, "â„³áµ’áµ–[$(a.label)]")
    elseif isD(a)
        print(io, "ğ’Ÿ[$(a.label)]")
    end
end

function Base.isless(a::IsingBimod, b::IsingBimod)
    return isless((a.type, a.label), (b.type, b.label)) # order ğ’ < â„³áµ’áµ– < â„³ < ğ’Ÿ, and then within each cat according to label
end

function Base.hash(a::IsingBimod, h::UInt)
    return hash(a.label, hash(a.type, h))
end

dim(a::IsingBimod) = dim(convert(IsingAnyon, a))
